
plugins {
  id 'java'
  id 'maven'
  id 'checkstyle'
  id 'eclipse'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
  mavenCentral()
  maven {
    url "http://oss.sonatype.org/content/groups/public/"
  }
}

mainClassName="me.lcw.jlb.Main"

project.archivesBaseName='jlb'

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.12'

  compile (
    "org.threadly:threadly:$threadlyVersion",
    "org.threadly:litesockets:$litesocketsVersion",
    "org.slf4j:slf4j-api:1.7.21",
    "org.slf4j:slf4j-simple:1.7.21",
    "com.google.code.gson:gson:2.7"
  )
}

compileJava {
  options.compilerArgs << "-Xlint:all"
}

compileTestJava {
  options.compilerArgs << "-Xlint:all" << "-Xlint:-deprecation" << "-Werror"
}

plugins.withType(JavaPlugin) {
  checkstyle.sourceSets = [sourceSets.main]
}

test {
  getReports().getJunitXml().setDestination(file("${buildDir}/reports/tests/xml"))
  getReports().getHtml().setDestination(file("${buildDir}/reports/tests/html"))
  setBinResultsDir(file("${buildDir}/reports/tests/bin"))
}

jar {
  manifest {
    attributes 'Implementation-Title': 'jlb', 'Implementation-Version': version
  }
  baseName = 'jlb'
}

javadoc {
  source = sourceSets.main.allJava
  options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}
